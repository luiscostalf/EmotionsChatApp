plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

group='com.github.fingerprintjs'

def libraryOutputName(String variantName) {
    "fingerprint-android-${project.VERSION_NAME}-${variantName}.aar"
}

publishing {
    publications {
        fpRelease(MavenPublication) {
            groupId = 'com.github.fingerprintjs'
            artifactId = "fingerprint-android"
            version = project.VERSION_NAME
            afterEvaluate {
                from components.release
            }
        }
    }
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }


    lint {
        abortOnError true
        warningsAsErrors true
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    namespace 'com.fingerprintjs.android.fingerprint'

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFileName.endsWith('.aar')) {
                outputFileName = libraryOutputName(variant.name)
            }
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    if (!it.name.contains("Test")) {
        kotlinOptions.freeCompilerArgs += "-Xexplicit-api=warning"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    androidTestImplementation'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
}